example3312_dat = dataobj {
   DATA_INPUT_VARIABLES{
      ID:{type=categorical}
      TIME:{type=continuous, units="h"}
      DOSE:{type=continuous, units="mg"}
      DV:{type=continuous, units="mg/L"}
   }# end DATA_INPUT_VARIABLES

   SOURCE{
      file="bo1.csv"
      inputformat=nonmemFormat
      ignore="#"
   }# end SOURCE   
}


example3312_par = parobj{

  PRIOR_PARAMETERS{

        # prior on "THETA"
		MU_POP_K:{value=0.1}	
        VAR_POP_K:{value=0.3}
   
        # prior on "OMEGA"
		a_OMEGA_K:{value=0.1}
		b_OMEGA_K:{value=0.1}

   } # end PRIOR_PARAMETERS

# <<<< New PRIOR_VARIABLE_DEFINITION block: 														      >>>
# <<<< it is used to define prior distributions and all the necessary parameters transformations. 	      >>>
# <<<< This block substitutes the STRUCTURAL and VARIABILITY blocks	for those parameters that have priors >>>
   PRIOR_VARIABLE_DEFINITION{

	  # prior on "THETA"     
      lMU_POP_K = log{MU_POP_K}	
	  lPOP_K ~ {type=normal, mean=lMU_POP_K, var=VAR_POP_K}
	  POP_K = exp{lPOP_K}

	  # prior on "OMEGA"	
      TAU_K ~ {type=gamma, shape=a_OMEGA_K, scale=b_OMEGA_K}
      OMEGA_K=1/TAU_K
          
    } # end PRIOR_VARIABLE_DEFINITION
    
# <<<< the STRUCTURAL and VARIABILITY blocks														      >>>
# <<<< are used to define the numerical values of the parameters that DO NOT HAVE priors.      	 	      >>>
# <<<< Optional constraints (lo and high values) and units may be defined as usual						  >>>
   STRUCTURAL{
      POP_V:{value=0.1}
      POP_T:{value=0.1}
    }
    
    VARIABILITY{
      OMEGA_V:{value=0.1,type=VAR}
      OMEGA_T:{value=0.1,type=VAR}
    }   
  
} # end of parameter object

example3312_mdl = mdlobj{

   MODEL_INPUT_VARIABLES{
      ID:{use=id,level=2}
      TIME:{use=idv,units="h"}
      DOSE:{type=continuous,use=covariate}
      DV:{type=continuous,use=dv,level=1,prediction=Y}
   }# end MODEL_INPUT_VARIABLES

# <<<< Relevant model parameters are selected from the PRIOR_VARIABLE_DEFINITION block 		>>>
# <<<< or from the STRUCTURAL and VARIABILITY blocks										>>>
# <<<< and listed in the STRUCTURAL_PARAMETERS and VARIABILITY_PARAMETERS blocks as usual 	>>>
   STRUCTURAL_PARAMETERS{
      POP_K
      POP_V
      POP_T
   }# end STRUCTURAL_PARAMETERS

   VARIABILITY_PARAMETERS{
      OMEGA_K
      OMEGA_V
      OMEGA_T
   }# end VARIABILITY_PARAMETERS


   RANDOM_VARIABLE_DEFINITION{
      eta_K ~ {type=normal, mean=0, var=OMEGA_K,level=ID}
      eta_V ~ {type=normal, mean=0, var=OMEGA_V,level=ID}
      eta_T ~ {type=normal, mean=0, var=OMEGA_T, level=ID}
      eps ~ {type=normal, mean=0, var=1,level=DV}
   }# end RANDOM_VARIABLE_DEFINITION

   INDIVIDUAL_VARIABLES{
      K : {type=linear, trans=log, pop = POP_K, ranEff=eta_K}
      V : {type=linear, trans=log, pop = POP_V, ranEff=eta_V}
      TAU : {type=linear, trans=log, pop = POP_T, ranEff=eta_T}
      SD_ADD = sqrt(1/TAU)
   }# end INDIVIDUAL_VARIABLES

   MODEL_PREDICTION{   
      C=DOSE/V*exp(-K*TIME)
   }# end MODEL_PREDICTION   
   
   OBSERVATION{
      Y : {type=continuous, error=additiveError(additive=SD_ADD), eps=eps, prediction= C}
   }# end OBSERVATION  

} # end of model object

example3312_task = taskobj{   
    ESTIMATE { 
    	target=BUGS_CODE  
	} 
} # end task
	
example3312 = mog{
	example3312_dat
	example3312_mdl
	example3312_par
	example3312_task
}

