example334_dat = dataobj {
   DATA_INPUT_VARIABLES{
      ID:{type=categorical}
      TIME:{type=continuous, units="h"}
      DOSE:{type=continuous, units="mg"}
      DV:{type=continuous, units="mg/L"}
   }# end DATA_INPUT_VARIABLES

   SOURCE{
      file="bo1.csv"
      inputformat=nonmemFormat
      ignore="#"
   }# end SOURCE
}

example334_par = parobj{

  PRIOR_PARAMETERS{
		# prior on "THETA"
	 	MU_POP_K:{value=0.1} # median
		MU_POP_V:{value=8}
		VAR_POP_K:{value=0.3}
        VAR_POP_V:{value=0.3}

		# prior on "OMEGA"
        a_OMEGA_T:{value=0.3}
		b_OMEGA_T:{value=0.3}      
        
# <<<< Matrix definition ?>>> 
        R:{value=c(1,0.1,0.1,1),dim=c(2,2)}
        rho:{value=2} 
        
        # prior on "SIGMA"
        a_POP_T:{value=0.1}
		b_POP_T:{value=0.1}
   } # end PRIOR

# <<<< New PRIOR_VARIABLE_DEFINITION block: 														      >>>
# <<<< it is used to define prior distributions and all the necessary parameters transformations. 	      >>>
# <<<< This block substitutes the STRUCTURAL and VARIABILITY blocks	for those parameters that have priors >>>
   PRIOR_VARIABLE_DEFINITION{

	    # prior on "THETA"     
        lMU_POP_K = log{MU_POP_K}
        lMU_POP_V = log{MU_POP_V}		
	    lPOP_K ~ {type=normal, mean=lMU_POP_K, var=VAR_POP_K}
	    lPOP_V ~ {type=normal, mean=lMU_POP_V, var=VAR_POP_V}
	    POP_K = exp{lPOP_K}
	    POP_V = exp{lPOP_V}	
	  
	    # prior on "OMEGA"	
		OMEGA_P ~ {type=wishart, scaleMatrix=R, nu=rho}

		TAU_T ~ {type=gamma, shape=a_OMEGA_T, scale=b_OMEGA_T}
        OMEGA_T = 1/TAU_T 
        
        # prior on "SIGMA"  
        POP_T ~ {type=gamma, shape=a_POP_T, scale=b_POP_T} 
	            
    } # end PRIOR_VARIABLE_DEFINITION

} # end of parameter object

example334_mdl = mdlobj{

   MODEL_INPUT_VARIABLES{
      ID:{use=id,level=2}
      TIME:{use=idv,units="h"}
      DOSE:{type=continuous,use=covariate}
      DV:{type=continuous,use=dv,level=1,prediction=Y}
   }# end MODEL_INPUT_VARIABLES


# <<<< Relevant model parameters are selected from the PRIOR_VARIABLE_DEFINITION block  
# <<<< and listed in the STRUCTURAL_PARAMETERS and VARIABILITY_PARAMETERS blocks as usual >>>
   STRUCTURAL_PARAMETERS{
      lPOP_K
      lPOP_V
      POP_T
   }# end STRUCTURAL_PARAMETERS

   VARIABILITY_PARAMETERS{
      OMEGA_T
      OMEGA_P
   }# end VARIABILITY_PARAMETERS


   RANDOM_VARIABLE_DEFINITION{
# <<<< Creating a vector: v = c(el1,el2) ?>>>  
      lPOP_P = c(lPOP_K,lPOP_V)
      lP ~ {type=multivariateNormal, mean=lPOP_P, cov=OMEGA_P} 
      eta_T ~ {type=normal, mean=0, var=OMEGA_T, level=ID}
      eps ~ {type=normal, mean=0, var=1,level=DV}
   }# end RANDOM_VARIABLE_DEFINITION

   INDIVIDUAL_VARIABLES{
# <<<< Accessing vector elements: el1 = v[1] ?>>>
	  K = exp(lP[1])
	  V = exp(lP[2])
      TAU : {type=linear, trans=log, pop = POP_T, ranEff=eta_T}
      SD_ADD = sqrt(1/TAU)
   }# end INDIVIDUAL_VARIABLES

   MODEL_PREDICTION{   
      C=DOSE/V*exp(-K*TIME)
   }# end MODEL_PREDICTION   
   
   OBSERVATION{
      Y : {type=continuous, error=additiveError(additive=SD_ADD), eps=eps, prediction= C}
   }# end OBSERVATION  

} # end of model object

example334_task = taskobj{   
    ESTIMATE { 
    	target=BUGS_CODE  
	} 
} # end task
	
example334 = mog{
	example334_dat
	example334_mdl
	example334_par
	example334_task
}

