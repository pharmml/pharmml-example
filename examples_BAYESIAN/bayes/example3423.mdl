example3423_dat = dataobj {
   DATA_INPUT_VARIABLES{
      ID:{type=categorical}
      TIME:{type=continuous, units="h"}
      DOSE:{type=continuous, units="mg"}
      DV:{type=continuous, units="mg/L"}
   }# end DATA_INPUT_VARIABLES

   SOURCE{
       file="bo1.csv"
       inputformat=nonmemFormat
       ignore="#"
   }# end SOURCE
}

# <<<< Added another data object for empirical/nonparametric prior definition>>>
example3423_prior_dat = dataobj {

   DATA_INPUT_VARIABLES{
      bins_POP_K
      p_POP_K
      data_POP_V      
   }# end DATA_INPUT_VARIABLES

   SOURCE{
# <<<< INLINE data definition. In alternative one can source a data file as usual>>>
        # file = "prior_bo1.txt"
        inputformat=rFormat 
		INLINE{
			list(  
			   bins_POP_K = c(2.006510,2.045465,2.084421,2.123377,2.162333,2.201288,2.240244,2.279200,2.318156,2.357111 ), 
			   p_POP_K = c(0.033333,0.100000,0.100000,0.200000,0.100000,0.066667,0.166667,0.100000,0.066667,0.066667 ),
			   data_POP_V = c(5.050013,5.050013,5.050013,5.050013,5.064166,5.064166,5.064166,5.064166,5.078318,5.078318 )
			   ) 
			         
		}# end INLINE
   }# end SOURCE
}

example3423_par = parobj{

# <<<< new PRIOR_INPUT_VARIABLES block for empirical/nonparametric priors.   >>>
# <<<< the source tag indicates the correct data object >>>
  PRIOR_INPUT_VARIABLES(source=example3423_prior_dat){
     # prior on "THETA"
     bins_POP_K 
     p_POP_K          
     data_POP_V     
# <<<< Is it sufficient to list them or "use"/"type"/.. is required like in MODEL_INPUT_VARIABLES?   >>>
  }# end DATA_INPUT_VARIABLES


   PRIOR_PARAMETERS{
        # prior on "OMEGA"
		a_OMEGA_K:{value=0.1}
		b_OMEGA_K:{value=0.1}
		a_OMEGA_V:{value=0.1}
		b_OMEGA_V:{value=0.1}
        a_OMEGA_T:{value=0.3}
		b_OMEGA_T:{value=0.3}
		
		# prior on "SIGMA"
        a_POP_T:{value=0.1}
		b_POP_T:{value=0.1}
   
   } # end PRIOR

# <<<< New PRIOR_VARIABLE_DEFINITION block: 														      >>>
# <<<< it is used to define prior distributions and all the necessary parameters transformations. 	      >>>
# <<<< This block substitutes the STRUCTURAL and VARIABILITY blocks	for those parameters that have priors >>>
   PRIOR_VARIABLE_DEFINITION{

      # prior on "THETA"   
# <<<< the nonparametric and empirical distributions do not exist in the current version >>>  
	  POP_K ~ {type=nonparametric, bins=bins_POP_K, probability=p_POP_K}
      POP_V ~ {type=empirical, data=data_POP_V}

	  # prior on "OMEGA"	          
      TAU_K ~ {type=gamma, shape=a_OMEGA_K, scale=b_OMEGA_K}
      OMEGA_K=1/TAU_K
      TAU_V ~ {type=gamma, shape=a_OMEGA_V, scale=b_OMEGA_V}
      OMEGA_V=1/TAU_V 
      TAU_T ~ {type=gamma, shape=a_OMEGA_T, scale=b_OMEGA_T}
      OMEGA_T = 1/TAU_T 
      
      # prior on "SIGMA"  
      POP_T ~ {type=gamma, shape=a_POP_T, iscale=b_POP_T}        
   
    } # end PRIOR_VARIABLE_DEFINITION
} # end of parameter object

example3423_mdl = mdlobj{

   MODEL_INPUT_VARIABLES{
      ID:{use=id,level=2}
      TIME:{use=idv,units="h"}
      DOSE:{type=continuous,use=covariate}
      DV:{type=continuous,prediction=Y,use=dv,level=1}
   }# end MODEL_INPUT_VARIABLES

# <<<< Relevant model parameters are selected from the PRIOR_VARIABLE_DEFINITION block  
# <<<< and listed in the STRUCTURAL_PARAMETERS and VARIABILITY_PARAMETERS blocks as usual >>>
   STRUCTURAL_PARAMETERS{
      POP_K
      POP_V
      POP_T
   }# end STRUCTURAL_PARAMETERS

   VARIABILITY_PARAMETERS{
      OMEGA_K
      OMEGA_V
      OMEGA_T
   }# end VARIABILITY_PARAMETERS


   RANDOM_VARIABLE_DEFINITION{
      eta_K ~ {type=normal, mean=0, var=OMEGA_K,level=ID}
      eta_V ~ {type=normal, mean=0, var=OMEGA_V,level=ID}
      eta_T ~ {type=normal, mean=0, var=OMEGA_T, level=ID}
      eps ~ {type=normal, mean=0, var=1,level=DV}
   }# end RANDOM_VARIABLE_DEFINITION

   INDIVIDUAL_VARIABLES{
      K : {type=linear, trans=log, pop = POP_K, ranEff=eta_K}
      V : {type=linear, trans=log, pop = POP_V, ranEff=eta_V}
      TAU : {type=linear, trans=log, pop = POP_T, ranEff=eta_T}
      SD_ADD = sqrt(1/TAU)
   }# end INDIVIDUAL_VARIABLES

   MODEL_PREDICTION{      
      C=DOSE/V*exp(-K*TIME)     
   }# end MODEL_PREDICTION
   
   OBSERVATION{
      Y : {type=continuous, error=additiveError(additive=SD_ADD), eps=eps, prediction= C}
   }# end OBSERVATION

} # end of model object

example3423_task = taskobj{   
    ESTIMATE { 
    	target=BUGS_CODE  
	} 
} # end task
	
example3423 = mog{
	example3423_dat
	example3423_mdl
	example3423_par
	example3423_task
}

